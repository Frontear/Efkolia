plugins {
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"

    id "maven-publish"
    id "signing"
    id "java"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
archivesBaseName = "Efkolia"
group = "com.github.frontear"
version = "0.1"

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.slf4j:slf4j-api:1.8.0-beta4'")
    implementation("org.apache.logging.log4j:log4j-slf4j18-impl:2.17.0")
    implementation("com.google.guava:guava:31.0.1-jre")
    implementation("com.google.code.gson:gson:2.8.9")

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.8.2")
}

java {
    withJavadocJar()
    withSourcesJar()
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "Efkolia"
                description = "A simple framework designed for Minecraft modding"
                url = "https://github.com/frontear/efkolia"

                scm {
                    url = "https://github.com/frontear/efkolia"
                    connection = "scm:https://frontear@github.com/frontear/efkolia.git"
                    developerConnection = "scm:git://github.com/frontear/efkolia.git"
                }

                licenses {
                    license {
                        name = "GNU General Public License v3.0"
                        url = "https://www.gnu.org/licenses/gpl-3.0.en.html"
                        distribution = "repo"
                    }
                }

                developers {
                    developer {
                        id = "Frontear"
                        name = "Ali Rizvi"
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}