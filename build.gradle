plugins {
    id "io.freefair.lombok" version "4.1.6"
    id "maven-publish"
    id "java"
}

sourceCompatibility = JavaVersion.VERSION_1_8
group = "org.frontear"
version = "0.1"

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.apache.logging.log4j:log4j-core:2.8.1")
    implementation("org.apache.logging.log4j:log4j-api:2.8.1")
    implementation("com.google.code.gson:gson:2.8.0")

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.5.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.5.2")
}

// https://docs.freefair.io/gradle-plugins/4.1.6/reference/#_lombok_plugins
lombok {
    config["lombok.addJavaxGeneratedAnnotation"] = "true"
    config["lombok.experimental.flagUsage"] = "WARNING"
    config["lombok.extensionMethod.flagUsage"] = "ERROR"
    config["lombok.fieldDefaults.flagUsage"] = "WARNING"
    config["lombok.nonNull.flagUsage"] = "ERROR"
    config["lombok.sneakyThrows.flagUsage"] = "WARNING"
    config["lombok.toString.doNotUseGetters"] = "true"
}

publishing {
    repositories {
        maven {
            url = "https://maven.pkg.github.com/frontear/efkolia"
            credentials {
                username = "frontear"
                password = System.getenv("ACCESS")// generate at https://github.com/settings/tokens
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

test {
    useJUnitPlatform()
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = "6.0.1"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}