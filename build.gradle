import java.util.concurrent.ThreadLocalRandom

plugins {
    id "io.codearte.nexus-staging" version "0.21.1"
    id "com.bmuschko.nexus" version "2.3.1"
    id "io.freefair.lombok" version "4.1.6"
    id "java"
}

sourceCompatibility = JavaVersion.VERSION_1_8
group = "com.github.frontear"
version = "0.2.5"

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.apache.logging.log4j:log4j-core:2.8.1")
    implementation("com.google.code.gson:gson:2.8.0")
    implementation("com.google.guava:guava:21.0")

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.5.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.5.2")
}

// https://docs.freefair.io/gradle-plugins/4.1.6/reference/#_lombok_plugins
lombok {
    config["lombok.addJavaxGeneratedAnnotation"] = "true"
    config["lombok.experimental.flagUsage"] = "WARNING"
    config["lombok.extensionMethod.flagUsage"] = "ERROR"
    config["lombok.fieldDefaults.flagUsage"] = "WARNING"
    config["lombok.sneakyThrows.flagUsage"] = "WARNING"
    config["lombok.toString.doNotUseGetters"] = "true"
}

//noinspection GroovyAssignabilityCheck
modifyPom {
    //noinspection GroovyAssignabilityCheck
    project {
        name = "Efkolia"
        description = "A simple framework designed for Minecraft modding"
        url = "https://github.com/frontear/efkolia"

        scm {
            url = "https://github.com/frontear/efkolia"
            connection = "scm:https://frontear@github.com/frontear/efkolia.git"
            developerConnection = "scm:git://github.com/frontear/efkolia.git"
        }

        licenses {
            license {
                name = "GNU General Public License v3.0"
                url = "https://www.gnu.org/licenses/gpl-3.0.en.html"
                distribution = "repo"
            }
        }

        developers {
            developer {
                id = "Frontear"
                name = "Ali Rizvi"
            }
        }
    }
}

test {
    final randBool = { ThreadLocalRandom.current().nextBoolean() }

    final props = [
        "-Defkolia.debug=${randBool()}",
        "-Defkolia.config.debug=${randBool()}",
        "-Defkolia.container.debug=${randBool()}",
        "-Defkolia.event.debug=${randBool()}",
        "-Defkolia.secure.random=${randBool()}"
    ]

    jvmArgs props
    useJUnitPlatform()
    testLogging.showStandardStreams = true

    doFirst {
        props.each { println it }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = "6.0.1"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}